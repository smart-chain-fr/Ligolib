#import "ligo-generic-fa2/lib/multi_asset/fa2.mligo" "FA2"
#import "./constants.jsligo" "Constants"
#import "./storage.jsligo" "Storage"
#import "./extension.jsligo" "Extension"
#import "./errors.jsligo" "Errors"

export type storage = Storage.t
type result = [list<operation>, storage]

export type mint_or_burn = [@layout:comb] {
   owner    : address,
   token_id : nat,
   amount_  : nat,
};

export type permit_params = [key, [signature, bytes]];
export type expiry_params = [address, [nat, option<bytes>]];

let create = ([param_create, s] : [[FA2.TokenMetadata.data, address, nat], storage]) : result => {
    let [metadata,owner,amount] = param_create;
    let _ = Extension.assert_admin(s.extension);
    let md = Storage.add_new_token(s.token_metadata, metadata.token_id, metadata);
    let s = Storage.set_token_metadata(s, md);
    let ledger = FA2.Ledger.increase_token_amount_for_user(s.ledger)(owner)(metadata.token_id)(amount);
    let new_s = FA2.Storage.set_ledger(s)(ledger);
    let supply = Extension.create_supply(new_s.extension.token_total_supply, metadata.token_id, amount);
    [Constants.no_operation, { ...new_s, extension : Extension.set_supply(s.extension, supply) }]
};

let mint = ([lst, s] : [list<mint_or_burn>, storage]) : result => {
    let _ = Extension.assert_admin(s.extension);
    let process_one = ([ledger_and_supply, param_mint] : [[FA2.Ledger.t, Extension.token_total_supply], mint_or_burn]) : [FA2.Ledger.t, Extension.token_total_supply] => {
        let [ledger,supply] = ledger_and_supply;
        let {owner,token_id,amount_} = param_mint;
        let _ = FA2.Storage.assert_token_exist(s)(token_id);
        [
            FA2.Ledger.increase_token_amount_for_user(ledger)(owner)(token_id)(amount_),
            Extension.increase_supply(supply, token_id, amount_)
        ]
    };
    let [ledger, supply] = List.fold_left(process_one, [s.ledger, s.extension.token_total_supply], lst);
    let s = FA2.Storage.set_ledger(s)(ledger);
    [Constants.no_operation, { ...s, extension : Extension.set_supply(s.extension, supply) }]
};

let burn = ([lst, s] : [list<mint_or_burn>, storage]) : result => {
   let _ = Extension.assert_admin(s.extension);
   let process_one = ([ledger_and_supply, param_burn] : [[FA2.Ledger.t, Extension.token_total_supply], mint_or_burn]) : [FA2.Ledger.t, Extension.token_total_supply] => {
      let [ledger,supply] = ledger_and_supply;
      let {owner,token_id,amount_} = param_burn;
      [
        FA2.Ledger.decrease_token_amount_for_user(ledger)(owner)(token_id)(amount_),
        Extension.decrease_supply(supply, token_id, amount_)
      ]
    };
   let [ledger, supply] = List.fold_left(process_one, [s.ledger, s.extension.token_total_supply], lst);
   let s = FA2.Storage.set_ledger(s)(ledger);
   [Constants.no_operation,{ ...s, extension : Extension.set_supply(s.extension, supply) }]
};

// TZIP-17
let permit = ([permits, s] : [list<permit_params>, storage]) : result => {
    let process_permit = ([ext, permit] : [Extension.t, permit_params]) : Extension.t => {
        let [pub_key, [sig, hash_]] = permit;
        let packed = Bytes.pack ([(Tezos.get_chain_id()), Tezos.get_self_address()], [ext.counter, hash_]);
        if (Crypto.check(pub_key, sig, packed)) {
            let sender_ = Tezos.address (Tezos.implicit_account (Crypto.hash_key(pub_key)));
            let permit_key = [sender_, hash_];
            let ext_modified = match (Big_map.find_opt(permit_key, ext.permits), {
                None: () => {
                    Extension.add_permit(ext, permit_key)
                },
                Some: (submission_timestamp: timestamp) => {
                    let _ = Extension._check_not_expired(s.extension, submission_timestamp, permit_key);
                    Extension.update_permit(ext, permit_key)
                }
            });
            return ext_modified;
        } else {
            return (Michelson `{FAILWITH}` as ((n: [string, bytes]) => Extension.t)) ([Errors.missigned, packed])
        }
    };
    let extension = List.fold_left(process_permit, s.extension, permits);
    [Constants.no_operation, { ...s, extension : extension }]
};

// TZIP-17
let set_expiry = ([p, s] : [expiry_params, storage]) : result => {
    let [user_address, [seconds, permit_hash_opt]] = p;
    let _check_max_expiry : unit = assert_with_error((seconds <= s.extension.max_expiry), Errors.max_seconds_exceeded);
    let _check_sender : unit = assert_with_error((Tezos.get_sender() == user_address), Errors.forbidden_expiry_update);
    match (permit_hash_opt, {
        None: () => {
            return [
                Constants.no_operation,
                {...s, extension : { ...s.extension, 
                    user_expiries : Big_map.add(user_address, (Some(seconds)), s.extension.user_expiries) }}
            ];                        
        },
        Some: (permit_hash: bytes) => {
            return [
                Constants.no_operation,
                { ...s, extension : {...s.extension, permit_expiries : Big_map.add(
                    [user_address, permit_hash],
                    (Some(seconds)),
                    s.extension.permit_expiries)
                }}
            ]
        }
    });
};

// TZIP-17 implementation of TZIP-12 Transfer
let transfer_permitted = ([t, s]: [FA2.transfer, storage]) : result => {
     let make_transfer = ([acc, t] : [[FA2.Ledger.t, Extension.t], FA2.transfer_from]) : [FA2.Ledger.t, Extension.t] => {
        let [ledger, ext] = acc;
        const [is_transfer_authorized, ext] = Extension.transfer_presigned(ext, t);
        const {from_, tx} = t;
        let apply_transfer = ([ledger, dst] : [FA2.Ledger.t, FA2.atomic_trans]) : FA2.Ledger.t => {
            let {token_id, amount, to_} = dst;
            let _ = FA2.Storage.assert_token_exist(s)(token_id);
            if (!is_transfer_authorized) {
                let _ = FA2.Operators.assert_authorisation(s.operators)(from_)(token_id);
                let ledger_modified = FA2.Ledger.decrease_token_amount_for_user(ledger)(from_)(token_id)(amount);
                let ledger_final = FA2.Ledger.increase_token_amount_for_user(ledger_modified)(to_)(token_id)(amount);
                return ledger_final
            } else {
                let ledger_modified = FA2.Ledger.decrease_token_amount_for_user(ledger)(from_)(token_id)(amount);
                let ledger_final = FA2.Ledger.increase_token_amount_for_user(ledger_modified)(to_)(token_id)(amount);
                return ledger_final
            }
        };
        let ledger = List.fold(apply_transfer, tx, ledger);
        [ledger, ext]
    };
    let [new_ledger, new_ext] = List.fold(make_transfer, t, [s.ledger, s.extension]);
    [Constants.no_operation, { ...s, ledger : new_ledger, extension : new_ext }]
};

let set_admin = ([addr, s]: [address, storage]) : result => { 
    [Constants.no_operation, { ...s, extension : Extension.set_admin(s.extension, addr) }]
};

export type parameter = [@layout:comb]
    ["Transfer", FA2.transfer] |
    ["Balance_of", FA2.balance_of] |
    ["Update_operators", FA2.update_operators] |
    ["Create_token", [FA2.TokenMetadata.data, address, nat]] |
    ["Mint_token", list<mint_or_burn>] |
    ["Burn_token", list<mint_or_burn>] |
    ["Permit", list<permit_params>] |
    ["SetExpiry", expiry_params] |
    ["SetAdmin", address];

export let main = ([p,s]: [parameter, storage]): result => {
    return match (p, {
        Transfer:         (p: FA2.transfer) => transfer_permitted(p,s),
        Balance_of:       (p: FA2.balance_of) => FA2.balance_of(p)(s),
        Update_operators: (p: FA2.update_operators) => FA2.update_ops(p)(s),
        Create_token:     (p: [create_token, address, nat]) => create(p, s),
        Mint_token:       (p: list<mint_or_burn>) => mint(p, s),
        Burn_token:       (p: list<mint_or_burn>) => burn(p, s),
        Permit:           (p: list<permit_params>) => permit(p, s),
        SetExpiry:        (p: expiry_params) =>set_expiry(p, s),
        SetAdmin:         (p: address) => set_admin(p, s)
    })
};

//     Off-chain views required by TZIP-17
//     Command to run to get the micheline expressions to put in the metadata:
//     ligo compile expression cameligo '_get_counter' \
//         --init-file src/main.mligo \
//         --project-root . \
//         --michelson-format json

let _get_default_expiry = ([_,s]: [unit, storage]) : nat => {
    return s.extension.default_expiry
};

let _get_counter = ([_,s]: [unit, storage]) : nat => {
    return s.extension.counter
};